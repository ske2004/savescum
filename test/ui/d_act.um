import (
  "rect.um"
  "th.um"
  "canvas.um"
  "../../src/d_util.um"
)

type Tool* = struct {
  id: str
  r: rect.Rect
}

type ToolList* = struct {
  tools: []Tool
}

type ActStatus* = struct {
  hover:  ^Tool
  press:  ^Tool
  active: ^Tool
  focus:  ^Tool
}

type ActViaMouse* = struct {
  mouse: th.Vf2
  pressing: bool 
  tabbed: bool
}

  fn (toollist: ^ToolList) get*(id: str): ^Tool {
  for i, tool^ in toollist.tools {
    if tool.id == id {
      return tool
    }
  }

  toollist.tools = append(toollist.tools, Tool{id: id})
  return &toollist.tools[len(toollist.tools)-1]
}

fn (a: ^ActStatus) update*(toollist: ^ToolList, v: ActViaMouse) {
  a.hover = null
  a.press = null

  if v.pressing == false {
    a.active = null
  } else {
    a.focus = null
  }

  for i, tool^ in toollist.tools {
    if d_util.rectVsPoint(tool.r, v.mouse) {
      a.hover = tool

      if v.pressing {
        a.press = tool
      }
    }
  }

  if a.active == null {
    a.active = a.press
  } else {
    a.focus = a.active
  }

  if v.tabbed {
    focusIndex := 0

    for i, tool^ in toollist.tools {
      if a.focus == tool {
        focusIndex = i+1
      }
    }

    a.focus = &toollist.tools[focusIndex%len(toollist.tools)]
  }
}

fn renderHint(tool: ^Tool) {
  dims := canvas.textSize(tool.id, 2)
  pos := tool.r.getPos()
  pos.y -= dims.y

  r := rect.fromVf2(pos, dims).shrink({-2, -2})

  canvas.drawRect(0x0000FFFF, r)
  canvas.drawText(tool.id, pos, 0xffffffff, 2)
}

fn renderDebugInfo*(actstatus: ^ActStatus) {
  if actstatus.focus != null {
    d_util.drawRectLines(0xFFFF00FF, actstatus.focus.r, 8)
  }
  if actstatus.active != null {
    d_util.drawRectLines(0xFF0000FF, actstatus.active.r, 6)
  }
  if actstatus.press != null {
    d_util.drawRectLines(0x00FF00FF, actstatus.press.r, 4)
  }
  if actstatus.hover != null {
    d_util.drawRectLines(0x0000FFFF, actstatus.hover.r, 2)
    renderHint(actstatus.hover)
  }
}

