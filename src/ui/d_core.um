import (
  "d_atoms.um"
  "../d_mouseinput.um"
  "../editor/d_drawcmd.um"
  "../d_util.um"
  "std.um"
  "rect.um"
)

type EventMouse = enum {
  left
  middle
  right
  scroll
  _count
}

type InputSink* = struct {
  sunk: [int(EventMouse._count)]bool
}

type Core* = struct {
  mouseState: d_mouseinput.MouseState
  frameNumber: uint
  inputSink: InputSink

  ctx: d_drawcmd.DrawContext
  tools: ^d_atoms.ToolNode
  currentTool: ^d_atoms.ToolNode
  maybeActiveTool: ^d_atoms.ToolNode
  activeTool: ^d_atoms.ToolNode
  hoveredTool: ^d_atoms.ToolNode
  focusedTool: ^d_atoms.ToolNode
}

type Events* = struct {
  sink: ^InputSink
  // The events the element itself has sunk, so we can return true if the event were to be queried again.
  acquired: InputSink
  mask: InputSink
  mouseState: d_mouseinput.MouseState
}

fn (core: ^Core) getToolState*(tool: ^d_atoms.ToolNode): d_atoms.ToolState {
  return d_atoms.ToolState{
    active: tool == core.activeTool,
    hovered: tool == core.hoveredTool,
    focused: tool == core.focusedTool
  }
}

fn (core: ^Core) maybeAcquireToolNode(name: str): ^d_atoms.ToolNode {
  if core.currentTool == null {
    core.currentTool = core.tools
  }

  if !validkey(core.currentTool.children, name) {
    return null
  }

  return core.currentTool.children[name]
}

fn (core: ^Core) acquireToolNode*(name: str): ^d_atoms.ToolNode {
  node := core.maybeAcquireToolNode(name)

  if node == null {
    tool := new(d_atoms.ToolNode)
    tool ^= d_atoms.ToolNode{children: map[str]^d_atoms.ToolNode{}, parent: core.currentTool}
    node = tool
    core.currentTool.children[name] = node    
  }

  return node
}

// Prunes all tools untouched last frame
fn (core: ^Core) pruneToolsRecursive*(node: ^d_atoms.ToolNode) {
  if node.lastFrameTouched == -1 {
    // Nodes with last frame -1 are not candidates to pruning (just a hardcoded value).
    // Here we automatically unfreeze the node because you need to freeze it every time.
    node.lastFrameTouched = core.frameNumber
    return
  }

  for key in node.children {
    core.pruneToolsRecursive(node.children[key])

    if node.children[key].lastFrameTouched < core.frameNumber {
      // Fix leak
      node.children[key].parent = null
      node.children = delete(node.children, key)
    }
  }
}

fn (core: ^Core) freezeTool*(name: str) {
  node := core.maybeAcquireToolNode(name)
  if node != null {
    node.lastFrameTouched = -1
  }
}

fn (core: ^Core) beginTool*(name: str, toolRect: rect.Rect): ^d_atoms.ToolNode {
  node := core.acquireToolNode(name)
  node.lastFrameTouched = core.frameNumber
  node.rect = toolRect
  node.layer = core.ctx.currentLayerId
  core.currentTool = node
  isOverMaybeActiveTool := true

  if core.maybeActiveTool != null {
    isOverMaybeActiveTool = node.layer >= core.maybeActiveTool.layer
  }
 
  if ok, scissorRect := core.ctx.getCurrentScissorRect(); ok {
    isOverMaybeActiveTool = isOverMaybeActiveTool && d_util.rectVsPoint(scissorRect, core.mouseState.pos)
  }

  if isOverMaybeActiveTool && d_util.rectVsPoint(node.rect, core.mouseState.pos) {
    core.maybeActiveTool = node
  }

  return core.currentTool
}

fn (core: ^Core) getCurrentToolState*(): d_atoms.ToolState {
  return core.getToolState(core.currentTool)
}

fn (core: ^Core) isCurrentToolActive*(): bool {
  return core.getCurrentToolState().active
}

fn (core: ^Core) isCurrentToolFocused*(): bool {
  return core.getCurrentToolState().focused
}

// Creates an input sink with all inputs set to 1.
// Can be used for masking.
fn initInputSinkSet*(): InputSink {
  sink := InputSink{}
 
  for i in sink.sunk {
    sink.sunk[i] = true
  }

  return sink
}

fn (core: ^Core) endTool*(): Events {
  if core.currentTool.parent == null {
    std.assert(false, "Parent node is null, beginTool/endTool mismatched!")
  }

  mask := InputSink{}
  if !d_atoms.areToolsRelated(core.currentTool, core.activeTool) {
    // Block all events if the current tool isn't hovered.
    mask = initInputSinkSet()
  }

  core.currentTool = core.currentTool.parent

  return Events{sink: &core.inputSink, mask: mask, mouseState: core.mouseState}
}

fn (core: ^Core) getProperty*(name: str, def: interface{}): interface{} {
  // Create a dummy tool where we'll store the data.
  tool := core.beginTool(name, {-100000, -100000, 0, 0})
  core.endTool()

  if !valid(tool.userdata) {
    tool.userdata = def
  }

  return tool.userdata
}

fn (core: ^Core) getPropertyLazy*(name: str, getter: fn (): any): any {
  // Create a dummy tool where we'll store the data.
  tool := core.beginTool(name, {-100000, -100000, 0, 0})
  core.endTool()

  if !valid(tool.userdata) {
    tool.userdata = getter()
  }

  return tool.userdata
}


fn sinkAcquireMaskEvent*(sink, acquire: ^InputSink, mask: InputSink, event: EventMouse): bool {
  e := int(event)

  if e < 0 || e >= int(EventMouse._count) {
    std.assert(false, sprintf("Invalid event id: %d", e))
  }

  // This event is masked so we can't process it.
  if mask.sunk[e] {
    return false
  }

  // This event was acquired, so it's successful.
  if acquire.sunk[e] {
    return true
  }

  // This event was already sunk. Can't sink it again.
  if sink.sunk[e] {
    return false
  }

  // Sink and acquire the event.
  sink.sunk[e] = true
  acquire.sunk[e] = true

  return true
}

fn sinkEvent*(events: ^Events, event: EventMouse): bool {
  return sinkAcquireMaskEvent(events.sink, &events.acquired, events.mask, event)
}

fn (events: ^Events) queryMouse*(btn: int, which: int): bool {
  if sinkEvent(events, EventMouse(btn)) == false {
    return false
  }

  var mouseButton: d_mouseinput.MouseButtonState

  switch btn {
    case 0: mouseButton = events.mouseState.lmb
    case 1: mouseButton = events.mouseState.mmb
    case 2: mouseButton = events.mouseState.rmb
    default: std.assert(false, sprintf("Invalid mouse button index %d", btn))
  }

  switch which {
    case d_mouseinput.down: return mouseButton.isDown
    case d_mouseinput.pressed: return mouseButton.isPressed
    case d_mouseinput.released: return mouseButton.isReleased
    default: std.assert(false, sprintf("Invalid mouse button property index %d", which))
  }

  return false
}

fn (events: ^Events) queryScroll*(): real {
  if sinkEvent(events, .scroll) == false {
    return 0
  }

  return events.mouseState.scroll.y
}
