import (
  "coll.um"
  "th.um"
  "rect.um"
  "canvas.um"
  "input.um"
  "image.um"
  "window.um"
  "../dbg/dbg.um"
  "color.um"
  "../gfx/rerp.um"
  "../gfx/background.um"
  "../data/resources.um"
  "../misc/util.um"
  "../misc/tagset.um"
  "world.um"
  "../shared/global.um"
  "../shared/defaults.um"
  "world_data.um"
  "font.um"
  "std.um"
  "audio.um"
  "../ui/notification.um"
  "controls.um"
)

type (
  ShapeType = enum {
    none
    block
    leftUpSlope
    rightUpSlope
    leftDownSlope
    rightDownSlope
  }

  Shape* = struct {
    rect: rect::Rect
    shape: ShapeType
  }

  Character* = struct {
    name: str
    id: str
    tags: tagset::TagSet
    originalRect: rect::Rect
    rect: rect::Rect
    vel: th::Vf2
    onGround: bool
    onSlope: bool
  }

  PAL = struct {
    time:               real
    dir:                int
    rotTime:            real
    walkTime:           real
    loadTime:           real
    visRect:            rect::Rect
    ent:                Character
    toggleActiveStatic: bool
    toggleActiveWater:  bool
  }

  Camera = struct {
    pos: th::Vf2
    scale: real
  }
)

// TPS 250
const (
  updateStep = 1/250.0
  updateStepMs* = 4
) 

cropBoulder        := rect::Rect{0, 0, 64, 128}
cropLeverOn        := rect::Rect{64, 64, 64, 64}
cropLeverOff       := rect::Rect{64, 0, 64, 64}
cropLeverStaticOn  := rect::Rect{256, 64, 64, 64}
cropLeverStaticOff := rect::Rect{256, 0, 64, 64}
cropLeverWaterOn   := rect::Rect{320, 128, 64, 64}
cropLeverWaterOff  := rect::Rect{320, 192, 64, 64}
cropControls       := rect::Rect{0, 128, 256, 256}

palStateStand := 0
palStateJump  := 1
palStateFall  := 2
palStandRect  := rect::Rect{128, 0, 128, 128}
palFaceRect   := rect::Rect{0, 0, 128, 128}
palMouthRect  := rect::Rect{41, 52, 22, 7}

var (
  // Resources
  imgObj: image::Image
  imgPAL: image::Image
  defaultFont: font::Font
  bgAudio: audio::Sound
  palStep: audio::Sound
  palJump: audio::Sound
  palLand: audio::Sound

  // PAL
  pal*: PAL
  palSaves: []PAL
  palCoyoteTime: int
  palFrown: bool = false
  palJitterDir: real
  palJitterTime: real
  palJitterCounter: int
  palWasOnGround: bool
  palBufferState: PAL
  palResetPoint: th::Vf2
  palInsideStatic: bool
  palNoSaveStack: bool

  // World
  metaBoulders: bool
  noSaveLoad: bool
  lockInput*: bool
  guides: []rect::Rect
  entities*: []Character
  world*: world::World
  camera: Camera
  toggleActive: bool
  toggleActiveVal: real

  // Misc
  time: real
  showDebug*: bool
  stopPreview: bool
  previewingLoad: bool
  lastUpdateTime: int = 0
  frameSkips*: int
  firstTime: bool = true
  rumbling: real
  controls: controls::Controls
  exited*:  bool
)

fn getCameraRect(): rect::Rect {
  return {pal.ent.rect.x+pal.ent.rect.w/2-global::screen.w/2, pal.ent.rect.y+pal.ent.rect.h/2-global::screen.h/2, global::screen.w, global::screen.h}
}

fn findGuideInPoint(p: th::Vf2): rect::Rect {
  for i, guide in guides {
    if coll::vf2ToRect(p, guide) {
      return guide.shrink({1, 1})
    }
  }

  return getCameraRect()
}

fn resetStates*() {
  toggleActive = false
  pal.toggleActiveStatic = metaBoulders
  pal.toggleActiveWater = true
  palSaves = {}
}

fn reset*() {
  pal.ent.rect.x = palResetPoint.x
  pal.ent.rect.y = palResetPoint.y
  pal.ent.vel.y = 0
  pal.ent.vel.x = 0
  resetStates()
}

fn getCameraTargetRect(): rect::Rect {

  guide := findGuideInPoint(pal.ent.rect.getPos())
  if controls.isPressed(.load) && len(palSaves) > 0 {
    guide = findGuideInPoint(palSaves[len(palSaves)-1].ent.rect.getPos())
  }
  cameraRect := getCameraRect()
  scale := util::min(guide.w/cameraRect.w, guide.h/cameraRect.h)
  maxScale := 1.0/(global::screen.w/800)
  if scale > maxScale {
    scale = maxScale
  }

  // put at center
  cameraRect.w *= scale
  cameraRect.h *= scale
  if controls.isPressed(.load) && len(palSaves) > 0 {
    lastSave := palSaves[len(palSaves)-1]

    cameraRect.x = lastSave.ent.rect.x+lastSave.ent.rect.w/2-cameraRect.w/2
    cameraRect.y = lastSave.ent.rect.y+lastSave.ent.rect.h/2-cameraRect.h/2
  } else {
    cameraRect.x = pal.ent.rect.x+pal.ent.rect.w/2-cameraRect.w/2
    cameraRect.y = pal.ent.rect.y+pal.ent.rect.h/2-cameraRect.h/2
  }

  return util::rectKeepInBounds(cameraRect, guide)
}

fn getTargetCamera*(): Camera {
  targetRect := getCameraTargetRect()
  return Camera{targetRect.getPos(), util::min(global::screen.w/targetRect.w, global::screen.h/targetRect.h)}
}

fn cameraControl() {
  props := getTargetCamera()

  camera.pos   = rerp::iVf2(camera.pos, props.pos, updateStep*10.0)
  camera.scale = rerp::iReal(camera.scale, props.scale, updateStep*10.0)
}

fn (camera: ^Camera) mapPoint(p: th::Vf2): th::Vf2 {
  p.x -= camera.pos.x
  p.y -= camera.pos.y
  p.x *= camera.scale
  p.y *= camera.scale
  return p
}

fn (camera: ^Camera) mapRect(r: rect::Rect): rect::Rect {
  r.x -= camera.pos.x
  r.y -= camera.pos.y
  r.x *= camera.scale
  r.y *= camera.scale
  r.w *= camera.scale
  r.h *= camera.scale
  return r
}

fn playerControl() {
  palCoyoteTime -= 1
  if palWasOnGround && !pal.ent.onGround {
    palCoyoteTime = 40
  }
  if !palWasOnGround && pal.ent.onGround {
    rumbling = 0.2
    palLand.start()
  }
  if controls.isPressed(.jump) && (pal.ent.onGround || palCoyoteTime > 0) {
    palCoyoteTime = 0
    palJump.start()
    pal.ent.vel.y = -3.65
  }
  if input::gamepad().id != -1 {
    stick := input::gamepad().stick(.left)

    if stick.x < -0.5 {
      pal.dir = 0
      pal.walkTime += updateStep*fabs(stick.x)
      pal.ent.vel.x = 2.5*stick.x
    } else if stick.x > 0.5 {
      pal.dir = 1
      pal.walkTime += updateStep*fabs(stick.x)
      pal.ent.vel.x = 2.5*stick.x
    }
  }
  if controls.isPressed(.left) {
    pal.dir = 0
    pal.ent.vel.x = -2.5
    pal.walkTime += updateStep
  }
  if controls.isPressed(.right) {
    pal.dir = 1
    pal.ent.vel.x = 2.5
    pal.walkTime += updateStep
  }

  if fabs(pal.ent.vel.x) < 0.1 {
    pal.walkTime = 0
  }

  if pal.ent.rect.y > 2e3 {
    reset()
  }

  if round(pal.walkTime*6+0.1) % 2 == 1 && pal.ent.onGround {
    palStep.setVol(0.5)
    palStep.setPitch(1.5+(std::rand()%10)/50.0)
    palStep.play()
  }

  palWasOnGround = pal.ent.onGround
}

fn entPhysics(ent: ^Character) {
  gravity := updateStep*60.0*0.1

  ent.vel.y += gravity

  if ent.vel.y > 10 {
    ent.vel.y = 10
  }

  ent.rect.x += ent.vel.x*updateStep*60.0
  ent.rect.y += ent.vel.y*updateStep*60.0

  ent.onGround = false
  ent.onSlope = false
  ent.vel.x = 0
}

fn collSortShapesByProximity(shapes: ^[]Shape, along: th::Vf2) {
  sort(shapes^, |along| {
    distA := a.rect.center().distanceTo(along)
    distB := b.rect.center().distanceTo(along)
    return distA < distB ? -1 : 1
  })
}

fn entHandleShape(ent: ^Character, s: Shape) {
  // Not a valid collision (probably pushed back by other blocks?)
  r := s.rect
  snap := util::rectSnap(ent.rect, r)

  switch s.shape {
  case .leftUpSlope:
    cornerX := ent.rect.x + ent.rect.w - r.x
    if cornerX > r.w {
      cornerX = r.w
    }
    cornerY := ent.rect.y + ent.rect.h - r.y
    hitY := r.h - ((r.h/r.w) * (cornerX))
    if hitY > r.h {
      hitY = r.h
    }

    if cornerY > hitY {
      ent.vel.y = 0
      ent.onGround = true
      ent.onSlope = true
      ent.rect.y += hitY-cornerY
    }
  case .rightUpSlope:
    cornerX := ent.rect.x - r.x
    if cornerX < 0 {
      cornerX = 0
    }
    cornerY := ent.rect.y + ent.rect.h - r.y
    hitY := (r.h/r.w) * (cornerX)

    if cornerY > hitY {
      ent.vel.y = 0
      ent.onGround = true
      ent.onSlope = true
      ent.rect.y += hitY-cornerY
    }
  case .leftDownSlope:
    cornerX := ent.rect.x + ent.rect.w - r.x
    if cornerX > r.w {
      cornerX = r.w
    }
    cornerY := ent.rect.y - r.y
    hitY := (r.h/r.w) * (cornerX)

    if cornerY < hitY {
      if ent.vel.y < 0 {
        ent.vel.y = 0
      }
      ent.rect.y += hitY-cornerY
    }
  case .rightDownSlope:
    cornerX := ent.rect.x - r.x
    if cornerX < 0 {
      cornerX = 0
    }
    cornerY := ent.rect.y - r.y
    hitY := r.h - ((r.h/r.w) * (cornerX))

    if cornerY < hitY {
      if ent.vel.y < 0 {
        ent.vel.y = 0
      }
      ent.rect.y += hitY-cornerY
    }
  case .block:
    if snap.x != 0 {
      ent.vel.x = 0
    } else if (snap.y > 0 && ent.vel.y < 0) || (snap.y < 0 && ent.vel.y > 0) {
      ent.vel.y = 0
    }

    if snap.y < 0 {
      ent.onGround = true
    }
    ent.rect.x += snap.x
    ent.rect.y += snap.y
  }
}

fn entVsShape(ent: ^Character, s: Shape): bool {
  r := s.rect
  
  return coll::rectToRect(ent.rect, r)
}

fn entConstructCollidingSolids(ent: ^Character): []Shape {
  tangents := world.getTangentTiles(rect::Rect{ent.rect.x-1, ent.rect.y-1, ent.rect.w+1, ent.rect.h+1})
  solids := []Shape{}
  nonBlockSolids := []Shape{}

  for i, entity^ in entities {
    if entity.id == ent.id {
      continue
    }
    if coll::rectToRect(ent.rect, entity.rect) && entity.tags.isSet("boulder") {
      solids = append(solids, Shape{entity.rect, .block})
    }
  }

  for i, tangent in tangents {
    shapeRect := rect::Rect{tangent.x*(world.tileset.cs.x/2), tangent.y*(world.tileset.cs.y/2), world.tileset.cs.x/2, world.tileset.cs.y/2}
    shapeType := ShapeType.none

    switch tangent.id {
      // slab
      case 25: shapeType = .block
        shapeRect.y += world.tileset.cs.y/4
        shapeRect.h /= 2
      case 30: if toggleActive { shapeType = .block }
      case 31: if !toggleActive { shapeType = .block }
      case 1, 5, 7, 29, 33, 34, 35:   shapeType = .block
      case 9:   shapeType = .leftUpSlope
      case 10:  shapeType = .rightUpSlope
      case 13:  shapeType = .leftDownSlope
      case 14:  shapeType = .rightDownSlope
      case 15:  shapeType = .leftUpSlope
        shapeRect.h /= 2
      case 16:  shapeType = .rightUpSlope
        shapeRect.h /= 2
      case 4:   shapeType = .leftUpSlope
        shapeRect.y += world.tileset.cs.y/4
        shapeRect.h /= 2
      case 8:   shapeType = .rightUpSlope
        shapeRect.y += world.tileset.cs.y/4
        shapeRect.h /= 2
      case 11:  shapeType = .rightDownSlope
        shapeRect.h /= 2
      case 12:  shapeType = .leftDownSlope
        shapeRect.h /= 2
    }

    if shapeType == .block {
      solids = append(solids, Shape{shapeRect, shapeType})
    } else {
      nonBlockSolids = append(nonBlockSolids, Shape{shapeRect, shapeType})
    }
  }


  collSortShapesByProximity(&solids, ent.rect.center())
  collSortShapesByProximity(&nonBlockSolids, ent.rect.center())
  solids = append(solids, nonBlockSolids)
  return solids
}

fn entCheckCollision(ent: ^Character): bool {
  solids := entConstructCollidingSolids(ent)

  for i, shape in solids {
    // FIXME: This is a shitty hotfix to prevent misdetecting invalid palSaves.
    shape.rect = shape.rect.shrink({0.01, 0.01})
    if entVsShape(ent, shape) {
      return true
    }
  }

  return false
}

fn entSolveCollision(ent: ^Character) {
  solids := entConstructCollidingSolids(ent)

  for i, solid in solids {
    if entVsShape(ent, solid) {
      entHandleShape(ent, solid)
    }
  }
}

fn _drawRectTangents(r: rect::Rect) {
  solids := entConstructCollidingSolids(&pal.ent)

  for i, solid in solids {
    rec := camera.mapRect(solid.rect)
    switch solid.shape {
    case .block:
      canvas::drawRect(0x00FF0055, rec)
    case .leftUpSlope, .rightDownSlope:
      canvas::drawLine(0x00FF0055, th::Vf2{rec.x, rec.y+rec.h}, th::Vf2{rec.x+rec.w, rec.y}, 2)
    case .rightUpSlope, .leftDownSlope:
      canvas::drawLine(0x00FF0055, th::Vf2{rec.x, rec.y}, th::Vf2{rec.x+rec.w, rec.y+rec.h}, 2)
    }
    canvas::drawText(sprintf("%v", i), th::Vf2{rec.x, rec.y}, th::white, camera.scale)
  }
}

fn convertEntityDescToCharacter(ent: world_data::EntityDesc): Character {
  return Character{name: ent.name, id: ent.id, tags: tagset::mk(ent.tags), rect: ent.rect}
}

fn findEntity*(id: str): ^Character {
  for i in entities {
    if entities[i].id == id {
      return &entities[i]
    }
  }

  return null
}

fn start*(worldData: world_data::WorldData): std::Err {
  exited = false
  stopPreview = false
  palNoSaveStack = worldData.noSaveStack
  previewingLoad = false
  bgAudio.seekToFrame(0)
  // bgAudio.play()
  lastUpdateTime = th::time
  toggleActive = false
  pal.toggleActiveWater = true
  lockInput = false
  metaBoulders = false

  world = worldData.world


  pal.rotTime = 0
  pal.walkTime = 0
  pal.dir = 0

  entities = []Character{}
  guides = []rect::Rect{}

  hasPlayer := false

  for i, ent in worldData.entities {
    chr := convertEntityDescToCharacter(ent)
    chr.originalRect = chr.rect

    if chr.tags.isSet("player") {
      r := rect::Rect{
        chr.rect.x,
        chr.rect.y,
        world.tileset.cs.x/2.4,
        world.tileset.cs.y/2
      }
      chr.originalRect = r
      chr.rect = r
      pal.ent = chr
      pal.dir = 1
      palResetPoint.x = pal.ent.rect.x
      palResetPoint.y = pal.ent.rect.y
      hasPlayer = true
    } else if chr.tags.isSet("Comment") {
      if chr.tags.isSet("Show") {
        dbg::logt(sprintf("Comment: %v", chr.name))
        entities = append(entities, chr)
      }
    } else if chr.tags.isSet("meta_boulders") {
      metaBoulders = chr.tags.param("meta_boulders") == "on"
    } else if chr.tags.isSet("Guide") {
      guides = append(guides, chr.rect)
    } else {
      entities = append(entities, chr)
    }
  }

  pal.toggleActiveStatic = metaBoulders
  palSaves = []PAL{}
  camera = getTargetCamera()

  if !hasPlayer {
    return std::error(1, "No player entity found")
  }

  if firstTime {
    notification::put({.info, "Welcome to the demo!\nPress 'Z' to load the last save,\n      'X' to save the current state,\n      'R' to reset the level.\n      'C' to preview a load."})
    firstTime = false
  }

  return {}
}

fn stop*() {
  bgAudio.stop()
}

fn setup*() {
  global::screen = {0, 0, 1200, 900}
  window::setup("SAVESCUM", trunc(global::screen.w), trunc(global::screen.h))
}

fn init*(rl: ^resources::ResourceLoader) {
  controls = defaults::controls()
  bgAudio = rl.loadAudio("data/audio/soundtrack/scene1.ogg")
  palStep = rl.loadAudio("data/audio/sfx/pal_walk.ogg")
  palJump = rl.loadAudio("data/audio/sfx/pal_jump.ogg")
  palLand = rl.loadAudio("data/audio/sfx/pal_land.ogg")

  // bgAudio.setLooping(true)
  defaultFont = rl.loadFont("data/font/FairfaxHD.ttf", 24*2, .linear)
  imgObj = rl.loadImage("data/img/obj_interactive.png", .linear)
  imgPAL = rl.loadImage("data/img/chr_pal.png", .linear)
}

fn updateEntities() {
  entPhysics(&pal.ent)
  if pal.ent.vel.y < 0 && palJitterDir >= 0 {
    palJitterDir = -1
    palJitterTime = 0
  }
  if pal.ent.vel.y > 0 && palJitterDir <= 0 {
    palJitterDir = 1
    palJitterTime = 0
  }

  palJitterTime += updateStep

  if palJitterTime < 0.1 {
    palJitterCounter += 1
  } else {
    palJitterCounter = 0
  }
  entSolveCollision(&pal.ent)

  if pal.ent.vel.y > 3 {
    pal.rotTime += updateStep*100.0
  }
  if pal.ent.vel.y <= 0 {
    pal.rotTime = 0
  }
}

fn drawEntityCustomRect(ent: ^Character, r: rect::Rect) {
  color := th::blue
  if ent.onGround {
    color = th::red
  }

  canvas::drawRect(color, camera.mapRect(ent.rect))
}

fn drawPAL(state: int)

fn drawEntityObject*(name: str, tags: ^tagset::TagSet, rect: rect::Rect, scale: real): bool {
  if tags.isSet("player") {
    rect.w *= 2
    rect.h *= 2
    rect.y -= rect.h/2.2
    rect.x -= rect.w/6
    imgPAL.blit(palStandRect, rect)
    imgPAL.blit(palFaceRect, rect)
  } else if tags.isSet("lever") {
    if toggleActive {
      imgObj.blit(cropLeverOn, rect)
    } else {
      imgObj.blit(cropLeverOff, rect)
    }
  } else if tags.isSet("lever_s") {
    if pal.toggleActiveStatic {
      imgObj.blit(cropLeverStaticOn, rect)
    } else {
      imgObj.blit(cropLeverStaticOff, rect)
    }
  } else if tags.isSet("lever_w") {
    if pal.toggleActiveWater {
      imgObj.blit(cropLeverWaterOn, rect)
    } else {
      imgObj.blit(cropLeverWaterOff, rect)
    }
  } else if tags.isSet("water") {
    canvas::drawRect(0x0099FF66, util::rectShrink(rect, -2*scale))
  } else if tags.isSet("static") {
    canvas::drawRect(0xFFFF0066, util::rectShrink(rect, -2*scale))
  } else if tags.isSet("boulder") {
    imgObj.blit(cropBoulder, rect)
  } else if tags.isSet("exit") {
    background::drawBackgroundSquare(rect, 1000000-time, 0x114411FF, 0x227722FF, {0.5*scale, 0.5*scale})
  } else if tags.isSet("tele") {
    rect = util::rectShrink(rect, 5*scale)
    background::drawBackgroundSquare(rect, 1000000-time, 0x111144FF, 0x222277FF, {0.5*scale, 0.5*scale})
  } else if tags.isSet("tele_off") {
    rect = util::rectShrink(rect, 5*scale)
    background::drawBackgroundSquare(rect, time, 0x111111FF, 0x222222FF, {0.5*scale, 0.5*scale})
  } else if tags.isSet("controls") {
    imgObj.blit(cropControls, rect)
  } else if tags.isSet("Comment") {
    defaultFont.draw(name, rect.getPos().addf(1*camera.scale), 0x000000FF, scale/2)
    defaultFont.draw(name, rect.getPos(), 0xFFFFFFFF, scale/2)
  } else if tags.isSet("first_save") || tags.isSet("force_load") {
    // NOTHING
  } else {
    return false
  }
  return true
}

fn drawEntity(ent: ^Character) {
  destRect := util::rectShrink(camera.mapRect(ent.rect), -1*camera.scale)

  if !drawEntityObject(ent.name, &ent.tags, destRect, camera.scale) {
    drawEntityCustomRect(ent, ent.rect)
  }
}

fn drawPAL2(state: int, pal: ^PAL, tint: uint32) {
  pal.loadTime -= th::delta/1000.0
  if pal.loadTime < 0 {
    pal.visRect = pal.ent.rect
  } else {
    pal.visRect = rerp::iRect(pal.visRect, pal.ent.rect, th::delta/100.0)
  }

  at := pal.visRect.getPos()
  bodyRect := rect::Rect{palFaceRect.x+palFaceRect.w*(state+1), palFaceRect.y, palFaceRect.w, palFaceRect.h}
  offs := 0.0
  if pal.dir == 0 {
    offs -= 24.5
  } else {
    offs -= 12.5
  }

  palDestRect := camera.mapRect({at.x+offs, at.y-29, palFaceRect.w/2, palFaceRect.h/2})
  palMouthDestRect := camera.mapRect({at.x+offs+41-21, at.y-29+52-22.25, 22/2, -7.05/2})

  if pal.dir == 0 {
    // Mirror.
    palDestRect.x += palDestRect.w
    palDestRect.w *= -1

    palMouthDestRect.x += palMouthDestRect.w+1.5*camera.scale
  }

  palMouthDestRect.x -= 0.5
  palMouthDestRect.y += 0.5
  palMouthDestRect.h -= 1
  palMouthDestRect.w += 1

  imgPAL.blit(bodyRect, palDestRect, tint, pal.rotTime, palDestRect.getDims().divf(2))
  imgPAL.blit(palFaceRect, palDestRect, tint, pal.rotTime, palDestRect.getDims().divf(2))
  if palFrown {
    imgPAL.blit(palMouthRect, palMouthDestRect, tint, pal.rotTime, palDestRect.getDims().divf(2))
  }
  if showDebug {
    canvas::drawText(sprintf("%d %d", trunc(at.x/32), trunc(at.y/32)), palDestRect.getPos(), th::red, 1)
  }
}

fn loadStateToChar(state: PAL) {
  old := pal
  oldVisRect := pal.visRect
  pal = state
  pal.visRect = oldVisRect
  pal.loadTime = 0.25
  if palInsideStatic {
    pal.toggleActiveStatic = old.toggleActiveStatic
  }
}

fn drawPAL(state: int) {
  drawPAL2(state, &pal, 0xFFFFFFFF)
}

fn getPALSpriteState(pal: PAL): int {
  if pal.ent.vel.y > 0 {
    return palStateFall
  } else if !pal.ent.onGround {
    return palStateJump
  } else if pal.walkTime == 0.0 {
    return palStateStand
  } else {
    switch trunc(pal.walkTime*6.0) % 2 {
      case 0: return palStateJump
      case 1: return palStateStand
    }
  }
  return -1
}

fn drawEntities() {
  for _, entity^ in entities {
    drawEntity(entity)
  }

  drawPAL(getPALSpriteState(pal))
}

fn drawEntityDebugRects() {
  for i, entity in entities {
    canvas::drawRect(color::alpha(th::blue, 0.4), camera.mapRect(entity.rect))
  }
  canvas::drawRect(color::alpha(th::blue, 0.4), camera.mapRect(pal.ent.rect))
}

fn canActivateLever(): bool {
  can := true
  toggleActive = !toggleActive

  if entCheckCollision(&pal.ent) {
    can = false
  }

  toggleActive = !toggleActive
  return can
}

fn canActivateStaticLever(): bool {
  can := true
  pal.toggleActiveStatic = !pal.toggleActiveStatic

  if entCheckCollision(&pal.ent) {
    can = false
  }

  pal.toggleActiveStatic = !pal.toggleActiveStatic
  return can
}

fn canActivateWaterLever(): bool {
  can := true
  pal.toggleActiveWater = !pal.toggleActiveWater

  if entCheckCollision(&pal.ent) {
    can = false
  }

  pal.toggleActiveWater = !pal.toggleActiveWater
  return can
}

fn tele*(to: str) {
  ent := findEntity(to)
  if ent == null {
    dbg::logt("Error: Destination entity doesn't exist")
  } else {
    if !entCheckCollision(ent) {
      resetStates()
      pal.rotTime = 0
      pal.loadTime = 0.5
      pal.ent.rect.x = ent.rect.x+(ent.rect.w-pal.ent.rect.w)/2
      pal.ent.rect.y = ent.rect.y+(ent.rect.h-pal.ent.rect.h)/2
      palResetPoint.x = pal.ent.rect.x
      palResetPoint.y = pal.ent.rect.y
    }
  }
}

fn update*() {
  palFrown = palJitterCounter > 300

  toggleActiveVal = rerp::iReal(toggleActiveVal, real(int(toggleActive)), th::delta/1000.0*20.0)

  iterations := (th::time - lastUpdateTime) / updateStepMs
  lastUpdateTime += iterations * updateStepMs

  if iterations > 120 {
    frameSkips += iterations-120
    iterations = 120
  }

  for i := 0; i < len(entities); i++ {
    ent := &entities[i]

    if ent.tags.isSet("boulder") {
      power := real(int(pal.toggleActiveStatic))
      if ent.tags.isSet("inv") {
        power *= -1
      }
      boulderTargetRect := ent.originalRect.shift(th::Vf2{0, -(ent.originalRect.h+32)*power})
      ent.rect = rerp::iRect(ent.rect, boulderTargetRect, th::delta/100.0)
    }
  }

  if previewingLoad && controls.isJustReleased(.preview) {
    stopPreview = true
  }

  if pal.loadTime > 0 {
    time = rerp::iReal(time, pal.time, th::delta/100.0)
    for d := 0; d < iterations; d++ {
      cameraControl()
    }
    return
  }

  if stopPreview {
    palSaves = append(palSaves, pal)
    loadStateToChar(palBufferState)
    previewingLoad = false
    stopPreview = false
    return
  }

  if previewingLoad {
    return
  }

  pal.time += th::delta/1000.0
  time = pal.time

  for d := 0; d < iterations; d++ {
    playerControl()

    // UPDATE PLAYER & CAMERA //
    updateEntities()
    cameraControl()
  }

  if !lockInput {
    if !noSaveLoad {
      if controls.isJustPressed(.load) {
        
        if len(palSaves) > 0 {
          for i := len(palSaves)-1; i >= 0; i-- {
            if !entCheckCollision(&palSaves[i].ent) {
              loadStateToChar(palSaves[i])
              palSaves = delete(palSaves, i)
              break
            }
          }
        }

      }

      if controls.isJustPressed(.save) {
        if !(palNoSaveStack && len(palSaves) > 0) {
          palSaves = append(palSaves, pal)
        }
      }

      if controls.isJustPressed(.preview) {
        for i := len(palSaves)-1; i >= 0; i-- {
          if !entCheckCollision(&palSaves[i].ent) {
            previewingLoad = true
            palBufferState = pal
            loadStateToChar(palSaves[i])
            palSaves = delete(palSaves, i)
            break
          }
        }
      }
    }
    noSaveLoad = false
    palInsideStatic = false

    if controls.isJustPressed(.reset) {
      reset()
    }

    for i := 0; i < len(entities); i++ {
      ent := &entities[i]

      if ent.tags.isSet("exit") {
        if coll::rectToRect(ent.rect, pal.ent.rect) && controls.isJustPressed(.activate) {
          exited = true
        }
      }
      if ent.tags.isSet("lever") {
        if coll::rectToRect(ent.rect, pal.ent.rect) {
          if controls.isJustPressed(.activate) && pal.ent.onGround {
            if canActivateLever() {
              toggleActive = !toggleActive
            }
          }
        }
      }
      if ent.tags.isSet("lever_s") {
        if coll::rectToRect(ent.rect, pal.ent.rect) && pal.ent.onGround {
          if controls.isJustPressed(.activate) {
            if canActivateStaticLever() {
              pal.toggleActiveStatic = !pal.toggleActiveStatic
            }
          }
        }
      }
      
      if ent.tags.isSet("force_load") {
        if coll::rectToRect(ent.rect, pal.ent.rect) {
          if len(palSaves) > 0 {
            for i := len(palSaves)-1; i >= 0; i-- {
              if !entCheckCollision(&palSaves[i].ent) {
                loadStateToChar(palSaves[i])
                palSaves = delete(palSaves, i)
                // FIXME: This is a hack
                return
              }
            }
          }
        }
      }

      if ent.tags.isSet("first_save") {
        if coll::rectToRect(ent.rect, pal.ent.rect) {
          if len(palSaves) == 0 {
            palSaves = append(palSaves, pal)
          }
        }
      }

      if ent.tags.isSet("water") {
        if pal.toggleActiveWater {
          ent.rect.y = ent.originalRect.y
        } else {
          ent.rect.y = ent.originalRect.y+100000
        }
        if coll::rectToRect(ent.rect, pal.ent.rect) {
          noSaveLoad = true
        }
      }

      if ent.tags.isSet("static") {
        if coll::rectToRect(ent.rect, pal.ent.rect) {
          palInsideStatic = true
        }
      }

      if ent.tags.isSet("lever_w") {
         if coll::rectToRect(ent.rect, pal.ent.rect) && pal.ent.onGround {
          if controls.isJustPressed(.activate) { 
            if canActivateWaterLever() {
              pal.toggleActiveWater = !pal.toggleActiveWater
            }
          }
        }
      }

      if ent.tags.isSet("tele") {
        if coll::rectToRect(ent.rect, pal.ent.rect) && controls.isJustPressed(.activate) {
          dest := ent.tags.param("tele")
          tele(dest)
        }
      }
    }
  }
}

fn drawGuideRects() {
  for i, guide in guides {
    canvas::drawRectLines(th::yellow, camera.mapRect(guide))
  }
}

fn drawOutline(color: uint32, rect: rect::Rect) {
  canvas::drawRect(color, rect)
}

fn drawOutlines() {
  radiusPulsing := 3

  renderTiles := world.collectLayerRenderTiles(camera.mapPoint({0, 0}), 0, camera.scale)

  for i, renderTile in renderTiles {
    distance := renderTile.rect.getPos().distanceTo(pal.ent.rect.getPos())
    proximityMultiplier := util::clamp(1.0-(distance/512.0), 0.0, 1.0)

    if distance > 256 {
      proximityMultiplier /= 2
    }

    if global::plainBiscuit {
      proximityMultiplier = 0.7
    }

    green := color::hsv(0.3, proximityMultiplier, util::max(proximityMultiplier, 0.05))
    yellow := color::hsv(0.15, proximityMultiplier, util::max(proximityMultiplier, 0.05))
    darkyellow := color::hsv(0.15, proximityMultiplier, util::max(proximityMultiplier, 0.05)/2.0)

    if renderTile.id == 29 {
      drawOutline(green, camera.mapRect(util::rectShrink(renderTile.rect, -3)))
    }
    if (renderTile.id == 30) || (renderTile.id == 31) {
      radius := radiusPulsing
      drawOutline(0x000000FF, camera.mapRect(util::rectShrink(renderTile.rect, -radius-1)))
      if renderTile.id == (toggleActive ? 31 : 30) {
        drawOutline(darkyellow, camera.mapRect(util::rectShrink(renderTile.rect, -radius)))
      } else {
        drawOutline(yellow, camera.mapRect(util::rectShrink(renderTile.rect, -radius)))
      }
    }
  }
}

fn drawOverlays() {
  renderTiles := world.collectLayerRenderTiles(camera.mapPoint({0, 0}), 0, camera.scale)

  alpha := color::alpha(0xFFFFFF00, toggleActiveVal)
  alpha2 := color::alpha(0xFFFFFF00, 1-toggleActiveVal)

  for i, renderTile in renderTiles {
    switch renderTile.id {
      case 30:
        world.tileset.i.blit(world.tileset.rect(30), camera.mapRect(renderTile.rect))
        world.tileset.i.blit(world.tileset.rect(31), camera.mapRect(renderTile.rect), alpha)
      case 31:
        world.tileset.i.blit(world.tileset.rect(30), camera.mapRect(renderTile.rect))
        world.tileset.i.blit(world.tileset.rect(31), camera.mapRect(renderTile.rect), alpha2)
    }
  }
}

fn drawObjectsAt(state: PAL) {
  for i, entity in entities {
    if entity.tags.isSet("boulder") {
      power := real(int(state.toggleActiveStatic))
      if entity.tags.isSet("inv") {
        power *= -1
      }
      boulderTargetRect := entity.originalRect.shift(th::Vf2{0, -(entity.originalRect.h+32)*power})
      imgObj.blit(cropBoulder, camera.mapRect(boulderTargetRect), 0, 0xFFFFFF22)
    }
  }
}

fn drawRayCast() {
  origin := camera.mapPoint(pal.ent.rect.center())
  direction := input::getMousePos().sub(origin)


  worldOrigin := pal.ent.rect.center()
  worldDirection := direction.divf(camera.scale*100)

  color := 0x0000FFFF

  at := 0
  for i := 0; i < 100; i++ {
    worldOrigin = worldOrigin.add(worldDirection)

    ent := Character{}
    ent.rect = rect::Rect{worldOrigin.x, worldOrigin.y, 1, 1}
    at = i

    if entCheckCollision(&ent) {
      color = 0xFF0000FF
      break
    }
  }

  canvas::drawLine(0x00FF00FF, origin, origin.add(direction.divf(100.0/at)), 3)
  util::drawArrow(origin, origin.add(direction), color)
}

fn draw*() {
  // RENDER BG //
  input::gamepad().rumble(rumbling*5, rumbling*5)
  bgCam := rect::Rect{camera.pos.x, camera.pos.y, global::screen.w/camera.scale, global::screen.h/camera.scale}
  bgCam.x /= 2
  bgCam.y /= 2
  rumbling = rerp::iReal(rumbling, 0, th::delta/100.0)
  oldCamera := camera

  if global::plainBiscuit {
    canvas::drawRect(0x777777FF, global::screen)
  } else {
    background::draw(bgCam, time)
  }

  // RENDER WORLD //
  world.drawLayer(camera.mapPoint({0, 0}), -1, camera.scale)
  drawOutlines()
  world.drawLayer(camera.mapPoint({0, 0}), 0, camera.scale)
  drawOverlays()
  drawEntities()

  for i, state in palSaves {
    if !entCheckCollision(&state.ent) {
      drawPAL2(getPALSpriteState(state), &state, 0xFFFFFF44)
    } else {
      drawPAL2(getPALSpriteState(state), &state, 0xFF000044)
    }
    drawObjectsAt(state)
  }

  world.drawLayer(camera.mapPoint({0, 0}), 1, camera.scale)

  // DEBUG OVERLAYS //
  if showDebug {
    _drawRectTangents(pal.ent.rect)
    drawGuideRects()
    canvas::drawRectLines(th::green, camera.mapRect(getCameraTargetRect()))
    drawEntityDebugRects()
    drawRayCast()
  }

  camera = oldCamera
}
