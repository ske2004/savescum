import (
  "input.um"
)


type (
  Control* = struct {
    opts: []input::Key
  }

  ControlKind* = enum {
    up
    down
    left
    right
    jump
    save
    load
    activate
    preview
    reset
    count
  }

  State* = enum {
    justPressed
    justReleased
    pressed
  }

  Controls* = [int(ControlKind.count)]Control
)

fn getInputState(k: input::Key, state: State): bool {
  switch state {
    case .justPressed:
      if input::isJustPressed(k) {
        return true
      }
    case .justReleased:
      if input::isJustReleased(k) {
        return true
      }
    case .pressed:
      if input::isPressed(k) {
        return true
      }
  }

  return false;
}

fn (ctls: ^Controls) getState*(ck: ControlKind, state: State): bool {
  for i, k in ctls[int(ck)].opts {
    if getInputState(k, state) {
      return true
    }
  }
  return false
}

fn (ctls: ^Controls) isJustPressed*(ck: ControlKind): bool {
  return ctls.getState(ck, .justPressed)
}

fn (ctls: ^Controls) isJustReleased*(ck: ControlKind): bool {
  return ctls.getState(ck, .justReleased)
}

fn (ctls: ^Controls) isPressed*(ck: ControlKind): bool {
  return ctls.getState(ck, .pressed)
}
