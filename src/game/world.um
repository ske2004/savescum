//  world.um
//
//  The world is split up into layers, which contain chunks.
//  There's 3 layers:
//    -1: the background layer, no collision checks are performed on it
//    0: the middleground layer, this is where the collisions are tested
//    1: the foreground layer, this is drawn over the character, no collision checks

import (
  "th.um"
  "rect.um"
  "atlas.um"
  "tilemap.um"
  "coll.um"
  "../misc/util.um"
  "../shared/global.um"
)

const (
  chunkW* = 320/16*2
  chunkH* = 240/16*2
)

type (
  TileProperties* = struct {
    invisible: bool
  }

  ChunkPos* = struct {
    x: int
    y: int
  }

  World* = struct {
    playerSpawnPos: th::Vf2
    tileProperties: map[int]TileProperties
    tileset: atlas::Atlas
    tilemap: [3]map[ChunkPos]tilemap::Tilemap
  }

  Tangent* = struct {
    x, y: int
    id: int
  }

  RenderTile* = struct {
    rect: rect::Rect
    id: int
  }
)

fn fromTileset*(ts: atlas::Atlas): World {
  return World{tileset: ts}
}

fn (world: ^World) setTile*(layer, tx, ty, tile: int) {
  layer++
  if layer < 0 || layer > 2 {
    return
  }

  // get or construct a chunk
  chunk_pos := ChunkPos{floor(tx/real(chunkW)), floor(ty/real(chunkH))}

  chunk := &world.tilemap[layer][chunk_pos]
  if chunk.w == 0 {
    chunk ^= tilemap::mk(make([]th::uu, chunkW*chunkH), chunkW, world.tileset, 1)
  }

  // make tile positions local
  tx = tx - chunk_pos.x * chunkW
  ty = ty - chunk_pos.y * chunkH

  chunk.edit(tx, ty, tile)
}

fn (world: ^World) getTile*(layer, tx, ty: int): int {
  layer++
  if layer < 0 || layer > 2 {
    return 0
  }

  chunk_pos := ChunkPos{floor(tx/real(chunkW)), floor(ty/real(chunkH))}
  if !validkey(world.tilemap[layer], chunk_pos) {
    return 0
  }

  chunk := &world.tilemap[layer][chunk_pos]

  // make tile positions local
  tx = tx - chunk_pos.x * chunkW
  ty = ty - chunk_pos.y * chunkH

  return chunk.cells[tx+ty*chunkW]
}

fn getTileSizeInPixels(world: ^World): th::Vf2 {
  return th::Vf2{world.tileset.cs.x/2, world.tileset.cs.y/2}
}

//
// getTangentTiles(
//      r: Rectangle to find tangent tiles around
// ): List of tangents, position of tile and it's id.
//
// NOTE: This function only checks middleground layer (layer 0)
//
fn (world: ^World) getTangentTiles*(r: rect::Rect): []Tangent {
  sz := getTileSizeInPixels(world)

  x, y, w, h := floor(r.x/sz.x), floor(r.y/sz.y), ceil(r.w/sz.x), ceil(r.h/sz.y)
  x2, y2 := x + w, y + h
  inity := y
  tangents := []Tangent{}

  for x <= x2 {
    y = inity
    for y <= y2 {
      tile := world.getTile(0, x, y)
      if tile > 0 {
        tangents = append(tangents, Tangent{x, y, tile})
      }
      y++
    }
    x++
  }

  return tangents
}

fn chunkVisible(world: ^World, chunk: ^tilemap::Tilemap, offset: th::Vf2, pan: th::Vf2, scale: real = 1.0): bool {
  tsz := getTileSizeInPixels(world)
  offset = offset.mulf(scale).add(pan)
  tileSize := tsz.mulf(scale)
  chunkSize := tileSize.mul(th::Vf2{chunkW, chunkH})
  chunkRect := rect::Rect{offset.x, offset.y, chunkSize.x, chunkSize.y}

  return coll::rectToRect(chunkRect, global::screen)
}

fn collectChunkRenderTiles(world: ^World, chunk: ^tilemap::Tilemap, offset: th::Vf2, pan: th::Vf2, scale: real = 1.0): []RenderTile {
  origOffset := offset
  tsz := getTileSizeInPixels(world)
  offset = offset.mulf(scale).add(pan)
  tileSize := tsz.mulf(scale)
  chunkSize := tileSize.mul(th::Vf2{chunkW, chunkH})
  chunkRect := rect::Rect{offset.x, offset.y, chunkSize.x, chunkSize.y}

  intersectionRect := util::rectIntersection(chunkRect, global::screen)

  startTileIndex := intersectionRect.getPos().sub(chunkRect.getPos()).div(tileSize)
  endTileIndex := intersectionRect.getEnd().sub(chunkRect.getPos()).div(tileSize)

  sx, sy, ex, ey := trunc(startTileIndex.x), trunc(startTileIndex.y), ceil(endTileIndex.x), ceil(endTileIndex.y)

  if ex > chunkW { ex = chunkW }
  if ey > chunkH { ey = chunkH }

  renderTiles := make([]RenderTile, chunkW*chunkH)

  i := 0
  for cx := sx; cx < ex; cx++ {
    for cy := sy; cy < ey; cy++ {
      tile := chunk.cells[cx+cy*chunkW]
      if tile > 0 {
        pos := th::Vf2{origOffset.x+cx*tsz.x, origOffset.y+cy*tsz.y}
        renderTiles[i] = RenderTile{{pos.x, pos.y, tsz.x, tsz.y}, tile}
        i++
      }
    }
  }

  return slice(renderTiles, 0, i)
}

fn (world: ^World) collectLayerRenderTiles*(pan: th::Vf2, layerId: int, scale: real = 1.0): []RenderTile {
  renderTiles := []RenderTile{}

  layerId++
  if layerId < 0 || layerId > 2 {
    return renderTiles
  }
  
  layer := &world.tilemap[layerId]

  for pos in layer {
    rpos := th::Vf2{pos.x, pos.y}
    renderTiles = append(renderTiles, collectChunkRenderTiles(world, &layer[pos], rpos.mul(th::Vf2{chunkW, chunkH}).mul(getTileSizeInPixels(world)), pan, scale))
  }

  return renderTiles
}


fn (world: ^World) drawRenderTiles*(renderTiles: []RenderTile, tint: uint32, pan: th::Vf2, scale: real) {
  for i, renderTile in renderTiles {
    if !world.tileProperties[renderTile.id].invisible {
      world.tileset.draw(world.tileset.coords(renderTile.id-1).item0,
        {
          p: {renderTile.rect.x*scale+pan.x, renderTile.rect.y*scale+pan.y},
          s: {renderTile.rect.w*scale/world.tileset.cs.x, renderTile.rect.h*scale/world.tileset.cs.y}
        }
      )
    }
  }
}

fn (world: ^World) drawLayer*(pan: th::Vf2, layerId: int, scale: real = 1.0, tint: uint32 = 0xFFFFFFFF) {
  tsz := getTileSizeInPixels(world)
  for p, chunk^ in world.tilemap[layerId+1] {
    offs := th::Vf2{p.x*chunkW*tsz.x, p.y*chunkH*tsz.y}

    if chunkVisible(world, chunk, offs, pan, scale) {
      chunk.draw({s: {scale/2, scale/2}, p: pan.add(offs.mulf(scale))})
    }
  }
}

fn (world: ^World) draw*(pan: th::Vf2) {
  for i := -1; i < 2; i++ {
    world.drawLayer(pan, i)
  }
}

