//  world.um
//
//  The world is split up into layers, which contain chunks.
//  There's 3 layers:
//    -1: the background layer, no collision checks are performed on it
//    0: the middleground layer, this is where the collisions are tested
//    1: the foreground layer, this is drawn over the character, no collision checks

import (
  "th.um"
  "rect.um"
  "../misc/util.um"
  "tileset.um"
  "../shared/global.um"
)

const (
  chunkW* = 320/16*2
  chunkH* = 240/16*2
)

type (
  Chunk* = [chunkW][chunkH]int
  ChunkPos* = struct {
    x: int
    y: int
  }
  World* = struct {
    playerSpawnPos: th::Vf2
    tileset: tileset::Tileset
    tilemap: [3]map[ChunkPos]Chunk
  }
  Tangent* = struct {
    x, y: int
    id: int
  }
  RenderTile* = struct {
    rect: rect::Rect
    id: int
  }
)

fn fromTileset*(ts: tileset::Tileset): World {
  return World{tileset: ts, tilemap: [3]map[ChunkPos]Chunk{map[ChunkPos]Chunk{}, map[ChunkPos]Chunk{}, map[ChunkPos]Chunk{}}}
}

fn (world: ^World) setTile*(layer, tx, ty, tile: int) {
  layer++
  if layer < 0 || layer > 2 {
    return
  }

  // get or construct a chunk
  chunk_pos := ChunkPos{floor(tx/real(chunkW)), floor(ty/real(chunkH))}

  chunk := &world.tilemap[layer][chunk_pos]

  // make tile positions local
  tx = tx - chunk_pos.x * chunkW
  ty = ty - chunk_pos.y * chunkH

  chunk[tx][ty] = tile
}

fn (world: ^World) getTile*(layer, tx, ty: int): int {
  layer++
  if layer < 0 || layer > 2 {
    return 0
  }

  chunk_pos := ChunkPos{floor(tx/real(chunkW)), floor(ty/real(chunkH))}
  if !validkey(world.tilemap[layer], chunk_pos) {
    return 0
  }

  chunk := &world.tilemap[layer][chunk_pos]

  // make tile positions local
  tx = tx - chunk_pos.x * chunkW
  ty = ty - chunk_pos.y * chunkH

  return chunk[tx][ty]
}


//
// getTangentTiles(
//      r: Rectangle to find tangent tiles around
// ): List of tangents, position of tile and it's id.
//
// NOTE: This function only checks middleground layer (layer 0)
//
fn (world: ^World) getTangentTiles*(r: rect::Rect): []Tangent {
  x, y, w, h := floor(r.x/world.tileset.tileSizeX), floor(r.y/world.tileset.tileSizeY), ceil(r.w/world.tileset.tileSizeX), ceil(r.h/world.tileset.tileSizeY)
  x2, y2 := x + w, y + h
  inity := y
  tangents := []Tangent{}

  for x <= x2 {
    y = inity
    for y <= y2 {
      tile := world.getTile(0, x, y)
      if tile > 0 {
        tangents = append(tangents, Tangent{x, y, tile})
      }
      y++
    }
    x++
  }

  return tangents
}

fn getTileSizeInPixels(world: ^World): th::Vf2 {
  return th::Vf2{world.tileset.tileSizeX, world.tileset.tileSizeY}
}

fn collectChunkRenderTiles(world: ^World, chunk: ^Chunk, offset: th::Vf2, pan: th::Vf2, scale: real = 1.0): []RenderTile {
  origOffset := offset
  tsz := getTileSizeInPixels(world)
  offset = offset.mulf(scale).add(pan)
  tileSize := tsz.mulf(scale)
  chunkSize := tileSize.mul(th::Vf2{chunkW, chunkH})
  chunkRect := rect::Rect{offset.x, offset.y, chunkSize.x, chunkSize.y}

  intersectionRect := util::rectIntersection(chunkRect, global::screen)

  startTileIndex := intersectionRect.getPos().sub(chunkRect.getPos()).div(tileSize)
  endTileIndex := intersectionRect.getEnd().sub(chunkRect.getPos()).div(tileSize)

  sx, sy, ex, ey := trunc(startTileIndex.x), trunc(startTileIndex.y), ceil(endTileIndex.x), ceil(endTileIndex.y)

  if ex > chunkW { ex = chunkW }
  if ey > chunkH { ey = chunkH }

  renderTiles := make([]RenderTile, chunkW*chunkH)

  i := 0
  for cx := sx; cx < ex; cx++ {
    for cy := sy; cy < ey; cy++ {
      tile := chunk[cx][cy]
      if tile > 0 {
        pos := th::Vf2{origOffset.x+cx*tsz.x, origOffset.y+cy*tsz.y}
        renderTiles[i] = RenderTile{{pos.x, pos.y, tsz.x, tsz.y}, tile}
        i++
      }
    }
  }

  return slice(renderTiles, 0, i)
}

fn (world: ^World) collectLayerRenderTiles*(pan: th::Vf2, layerId: int, scale: real = 1.0): []RenderTile {
  renderTiles := []RenderTile{}

  layerId++
  if layerId < 0 || layerId > 2 {
    return renderTiles
  }
  
  layer := &world.tilemap[layerId]

  for pos in layer {
    rpos := th::Vf2{pos.x, pos.y}
    renderTiles = append(renderTiles, collectChunkRenderTiles(world, &layer[pos], rpos.mul(th::Vf2{chunkW, chunkH}).mul(getTileSizeInPixels(world)), pan, scale))
  }

  return renderTiles
}


fn (world: ^World) drawRenderTiles*(renderTiles: []RenderTile, tint: uint32, pan: th::Vf2, scale: real) {
  prevTileId := -1

  for i, renderTile in renderTiles {
    cr := world.tileset.getCroppingRectId(renderTile.id)

    if prevTileId != renderTile.id {
      util::imageCropRect(world.tileset.image, cr)
      prevTileId = renderTile.id
    }

    if !world.tileset.tileProperties[renderTile.id].invisible {
      r := rect::Rect{renderTile.rect.x*scale+pan.x, renderTile.rect.y*scale+pan.y, renderTile.rect.w*scale, renderTile.rect.h*scale}

      world.tileset.image.draw({p: {r.x, r.y}, s: {r.w/cr.w, r.h/cr.h}}, tint)
    }
  }
}

fn (world: ^World) drawLayer*(pan: th::Vf2, layerId: int, scale: real = 1.0, tint: uint32 = 0xFFFFFFFF) {
  renderTiles := world.collectLayerRenderTiles(pan, layerId, scale)
  world.drawRenderTiles(renderTiles, tint, pan, scale)
}

fn (world: ^World) draw*(pan: th::Vf2) {
  for i := -1; i < 2; i++ {
    world.drawLayer(pan, i)
  }
}

