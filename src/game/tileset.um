import (
  "th.um"
  "image.um"
  "rect.um"
)

type (
  TileProperties* = struct {
    invisible: bool
  }

  Tileset* = struct {
    image: image::Image
    scale: real
    tileSizeX, tileSizeY: int
    scaledTileSizeX, scaledTileSizeY: real
    tileCountX, tileCountY: int
    tileProperties: map[int]TileProperties
  }
)

fn fromImage*(img: image::Image, tileSizeX, tileSizeY: int, scale: real): Tileset {
  dims := img.getDims()
  return Tileset{
    image: img,
    scaledTileSizeX: tileSizeX*scale, scaledTileSizeY: tileSizeY*scale,
    tileSizeX: tileSizeX, tileSizeY: tileSizeY,
    tileCountX: trunc(dims.x/(tileSizeX*scale)), tileCountY: trunc(dims.y/(tileSizeY*scale)), scale: scale}
}

fn (ts: ^Tileset) tileToPos*(x, y: int): int {
  return x + ts.tileCountX * y + 1
}

fn (ts: ^Tileset) posToTile*(pos: int): th::Vf2 {
  pos -= 1
  return th::Vf2{pos%ts.tileCountX, pos/ts.tileCountX}
}

fn (ts: ^Tileset) setTileProperties*(pos: int, props: TileProperties) {
  ts.tileProperties[pos] = props
}

fn (ts: ^Tileset) getCroppingRect*(x, y: int): rect::Rect {
  return {x*ts.scaledTileSizeX, y*ts.scaledTileSizeY,
          ts.scaledTileSizeX, ts.scaledTileSizeY}
}

fn (ts: ^Tileset) getCroppingRectId*(id: int): rect::Rect {
  id--
  return {(id%ts.tileCountX)*ts.scaledTileSizeX, (id/ts.tileCountX)*ts.scaledTileSizeY,
          ts.scaledTileSizeX, ts.scaledTileSizeY}
}

fn (ts: ^Tileset) selectTile*(x, y: int) {
  ts.image.cropRect(ts.getCroppingRect(x, y))
}

fn (ts: ^Tileset) selectTilePos*(pos: int) {
  pos -= 1
  ts.selectTile(pos%ts.tileCountX, pos/ts.tileCountX)
}

fn (ts: ^Tileset) isTileValid*(pos: int): bool {
  if pos < 1 || pos > (ts.tileCountX*ts.tileCountY) {
    return false
  }
  return true
}
